_enemy_loop_counter .equ generic_bytes+#89
NUMBER_OF_ACTORS .equ #6

.incsrc "src/play/stat_readout.6502"
.incsrc "src/play/load_room.6502"
.incsrc "src/play/actor_code.6502"

play_code:
  LDA paused
  BEQ +
    JMP +jumped_too_far
  +:
  LDA player_dead
  BEQ +
    JSR deadify
    LDA #0
    STA buttons
    STA button_ondown
  +:
  
    JSR inc_ai_timers
    
    JSR actor_tick
    
    JSR update_stat_readout
    JSR draw_room
    JSR draw_actors
    
    JSR check_room_move
    
    
    make_offer ; idk why I wrote this one as a macro.
    JMP ++
   +jumped_too_far:
    CMP #2
    BEQ +
      INC paused
      JSR greyify
    +:
    LDA button_ondown
    AND START_BUTTON
    BEQ +end_playcode
      LDA #0
      STA paused
      
      JSR ungreyify
  +end_playcode:
  RTS

check_room_move:

  LDA actors+ACT_X
  CMP #8
  BCS +
    LDA #240
    STA actors+ACT_X
    
    CLC
    DEC player_room
    
    JSR entered_room
  +:
  
  LDA actors+ACT_X
  CMP #248
  BCC +
    LDA #16
    STA actors+ACT_X
    
    CLC
    INC player_room
    
    JSR entered_room
  +:
  
  LDA actors+ACT_Y
  CMP #8
  BCS +
    LDA #170
    STA actors+ACT_Y
    
    LDA player_room
    CLC
    ADC #$10
    STA player_room
    
    JSR entered_room
  +:
  
  LDA actors+ACT_Y
  CMP #178
  BCC +
    LDA #16
    STA actors+ACT_Y
    
    LDA player_room
    SEC
    SBC #$10
    STA player_room
    
    JSR entered_room
  +:
  
  
  RTS
  
entered_room:
  LDA #1
  STA room_update_req
  
  LDA #0
  STA ai_state_msb
  
  LDA #$80
  STA ai_state_timer
  
  RTS

inc_ai_timers:
  
    LDA ai_state_timer
    CLC
    ADC #1
    STA ai_state_timer
    
    LDA ai_state_msb
    ADC #0
    STA ai_state_msb
    
    LDA ai_state_timer
    BNE ++
    
    ; THE FOLLOWING CODE IS ONLY RAN EVERY 256 FAMES
    
    LDA ai_state_msb
    CMP #1
    BEQ +set_default_state
    
    CMP #3 ; Three action cycle
    BNE ++ ; do noting on msb = 2
    
      LDA #0
      STA ai_state_msb
      
    
    +set_default_state:
    
      LDA #0 ; scatter
      STA warrior+ACT_AI_STATE
      STA theif+ACT_AI_STATE
      STA mage+ACT_AI_STATE
      STA tank+ACT_AI_STATE
      STA dragon+ACT_AI_STATE
    ++:
    RTS
