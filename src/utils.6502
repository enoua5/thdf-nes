rng: ;loads A and X
  LDX #8
  LDA rng_seed+0
--:
  ASL
  ROL rng_seed+1
  BCC +
  EOR #$2D
+:
  DEX
  BNE --
  STA rng_seed+0
  CMP #0
  RTS

pull_nt_buffer:
  LDX #$00 ;buffer pointer
  LDY #$00 ;entry length counter
  -:
    CPX nt_update_buf_wi
    BEQ finish_nt_update:
    CPY #$00
    BNE +
      ;we are at the start of a new entry
      LDY nt_buffer, X
      INX
      
      LDA $2002 ; read PPU status to reset the high/low latch
      
      LDA nt_buffer, X
      STA $2006 ; write the high byte of address
      INX
      DEY
      
      LDA nt_buffer, X
      STA $2006 ; write the low byte of address
      INX
      DEY
    +:
      LDA nt_buffer, X
      STA $2007 ; write to ppu
      INX
      DEY
      JMP -
      
  finish_nt_update:
  
  LDA #0
  STA nt_buffer_overflow
  STA nt_update_buf_wi
  STA nt_update_buf_wc
  
  RTS
  
_begin_nt_buffer_write:
  ;if the previous entry is unfinished, finish it
  LDA nt_update_buf_wi
  CMP nt_update_buf_wc
  BEQ +
    JSR _end_nt_buffer_write
  +:
  
  LDA nt_update_buf_wi
  CMP #$fb ;minimum space needed for a new entry
  BCC +
    LDA #1
    STA nt_buffer_overflow
  +:
  
  LDA nt_buffer_overflow
  BNE +
  
  LDX nt_update_buf_wi
  LDA #$00
  STA nt_buffer, X ;value will be updated to reflect length of update
                   ;change will occur when end_nt_buffer_write is called
  
  INX
  LDA nt_buf_in_hi
  STA nt_buffer, X
  
  INX
  LDA nt_buf_in_lo
  STA nt_buffer, X
  
  INX
  STX nt_update_buf_wc
  
  +:
  
  RTS
  
_write_to_nt_buffer:
  LDA nt_buffer_overflow
  BNE +
  
  LDA nt_buf_in_dt
  LDX nt_update_buf_wc
  STA nt_buffer, X
  INC nt_update_buf_wc
  
  ;buffer overflow check
  LDA nt_update_buf_wc
  CMP #$FE
  BNE +
    LDA #1
    STA nt_buffer_overflow
    JSR _end_nt_buffer_write
  +:
  RTS
  
_end_nt_buffer_write:
  ;find and write data lentgh
  LDA nt_update_buf_wc
  SBC nt_update_buf_wi
  LDX nt_update_buf_wi
  STA nt_buffer, X
  ;update `write initial` using `write current`
  LDX nt_update_buf_wc
  STX nt_update_buf_wi
  RTS
  
byte_to_dec:
  LDA arguments
  
  ;100's place
  LDX #0
  -:
    CMP #100
    BCC +
      INX
      ;SEC
      SBC #100
    JMP -
  +:
  STX returns+0
  
  ;10's place
  LDX #0
  -:
    CMP #10
    BCC +
      INX
      ;SEC
      SBC #10
    JMP -
  +:
  STX returns+1
  
  ;1's place
  STA returns+2
  
  RTS
  
check_sprite_overlap:
  LDX arguments+0
  LDA $0200,X ; sprite 1 Y
  STA returns+0
  INX
  INX
  INX
  LDA $0200,X ; sprite 1 X
  STA returns+1
  
  LDX arguments+1
  LDA $0200,X ; sprite 2 Y
  STA returns+2
  INX
  INX
  INX
  LDA $0200,X ; sprite 2 X
  STA returns+3

  ;;;;;;;;;;;;;;
  
  
  ; x1>x2+8
  LDA returns+3
  CLC
  ADC #8
  CMP returns+1
  BCC +no_overlap
  
  ; x2>x1+8
  LDA returns+1
  CLC
  ADC #8
  CMP returns+3
  BCC +no_overlap
  
  ; y1>y2+8
  LDA returns+2
  CLC
  ADC #8
  CMP returns+0
  BCC +no_overlap
  
  ; y2>y1+8
  LDA returns+0
  CLC
  ADC #8
  CMP returns+2
  BCC +no_overlap
  
  +overlap:
  LDA #1
  STA returns+0
  RTS
  +no_overlap:
  LDA #0
  STA returns+0
  RTS
  
modulo:
  ; check for divide by 0
  LDA arguments+1
  BNE +
    DEC arguments+1 ; set to 255.
                    ; This makes sense in the context of this game
                    ; as % 0 happens when the player maxes out a score
  +:
  LDA arguments+0
  SEC
  -:
    SBC arguments+1
  BCS -
  ADC arguments+1
  STA returns+0
  
  RTS
  
greyify:
  load_pal colorpal2
  ;LDA ppu_mask
  ;ORA #%00000001 ; grey-scale to indicate paused
  ;STA ppu_mask
  
  RTS

ungreyify:
  load_pal colorpal1
  ;LDA ppu_mask
  ;AND #%11111110 ; undo grey-scale
  ;STA ppu_mask
  RTS
  
  
  


MACRO call4 function, arg0, arg1, arg2, arg3
  LDA arg0
  STA arguments+0
  LDA arg1
  STA arguments+1
  LDA arg2
  STA arguments+2
  LDA arg3
  STA arguments+3
  
  JSR function
ENDM

MACRO call3 function, arg0, arg1, arg2
  LDA arg0
  STA arguments+0
  LDA arg1
  STA arguments+1
  LDA arg2
  STA arguments+2
  
  JSR function
ENDM

MACRO call2 function, arg0, arg1
  LDA arg0
  STA arguments+0
  LDA arg1
  STA arguments+1
  
  JSR function
ENDM

MACRO call1 function, arg0
  LDA arg0
  STA arguments+0
  
  JSR function
ENDM
